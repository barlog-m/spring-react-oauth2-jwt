buildscript {
	ext {
		kotlinVer = '1.1.51'
		springBootVer = '1.5.8.RELEASE'
		kotlinLoggingVer = '1.4.6'

		thymeleafVer = '3.0.9.RELEASE'

		icu4jVer = '60.1'

		okHttpVer = '3.9.0'
		jsonPathVer = '2.4.0'

		propdepsPluginVer = '0.0.10.RELEASE'
		versionPluginVer = '0.15.0'
		dockerPluginVer = '0.13.0'
	}

	repositories {
		jcenter()
		maven { url 'https://plugins.gradle.org/m2/' }
		maven { url 'http://repo.spring.io/plugins-release' }
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVer"
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVer"
		classpath "io.spring.gradle:propdeps-plugin:$propdepsPluginVer"
		// gradle dependencyUpdates -Drevision=release
		classpath "com.github.ben-manes:gradle-versions-plugin:$versionPluginVer"
		classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:$dockerPluginVer"
	}
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'propdeps'
apply plugin: 'propdeps-idea'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.palantir.docker'

mainClassName = 'li.barlog.app.AppKt'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = "1.8"
	}
}

sourceSets {
	main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
}

repositories {
	jcenter()
}

configurations {
	all*.exclude module : 'spring-boot-starter-tomcat'
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVer"
	compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVer"

	compile "io.github.microutils:kotlin-logging:$kotlinLoggingVer"

	compile 'com.fasterxml.jackson.module:jackson-module-kotlin'
	compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-undertow'

	compile "org.thymeleaf:thymeleaf:$thymeleafVer"
	compile "org.thymeleaf:thymeleaf-spring4:$thymeleafVer"

	compile 'org.springframework.boot:spring-boot-starter-actuator'

	compile 'org.springframework.boot:spring-boot-starter-security'

	compile 'org.springframework.security.oauth:spring-security-oauth2'
	compile 'org.springframework.security:spring-security-jwt'

	compile "com.ibm.icu:icu4j:$icu4jVer"

	testCompile 'org.springframework.security:spring-security-test'
	testCompile "com.jayway.jsonpath:json-path:$jsonPathVer"
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile "com.squareup.okhttp3:okhttp:$okHttpVer"

	kapt 'org.springframework.boot:spring-boot-configuration-processor'
	optional 'org.springframework.boot:spring-boot-configuration-processor'
}

task copyUI(type: Copy, dependsOn: ':ui:build') {
	from "${project(':ui').projectDir}/dist"
	into 'build/resources/main/static/ui'
}

jar {
	dependsOn copyUI

	archiveName 'app.jar'
}

bootRepackage.mustRunAfter clean

// https://github.com/palantir/gradle-docker
// gradle docker
docker {
	dependsOn jar, bootRepackage
	name 'app'
	tags 'latest'
	files jar.outputs
	dockerfile file('src/main/docker/Dockerfile')
	buildArgs([
		JAVA_OPTS: '-XX:-TieredCompilation -XX:+UnlockExperimentalVMOptions -XX:+UseCGroupMemoryLimitForHeap',
		PORT     : '8081'
	])
	pull true
}

task stage(dependsOn: ['build', 'clean'])
